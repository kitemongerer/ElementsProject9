class Main {

	function char getDirection(char direction) {
		var char tmp;
		let tmp = Keyboard.keyPressed();

		if ( (tmp = 87) | (tmp = 65) | (tmp = 83) | (tmp = 68) ) {
			
			//Make sure snake can't go backwards on itself
			if ( (direction = 87) & (tmp = 83) ) {
				return 0;
			}
			if ( (direction = 83) & (tmp = 87) ) {
				return 0;
			}
			if ( (direction = 65) & (tmp = 68) ) {
				return 0;
			}
			if ( (direction = 68) & (tmp = 65) ) {
				return 0;
			} else {
				return tmp;
			}
		}
		
		return 0;
	}

	function void welcome() {
		do Output.moveCursor(10, 24);
		do Output.printString("Welcome to Snake!");
		do Output.moveCursor(12, 18);
		do Output.printString("Press W, A, S, or D to start");

		return;
	}


	function void main() {
		var char direction;
		var Snake snake;
		var char tmp;

		let direction = 0;
		let snake = Snake.new();

		do Main.welcome();

		while(direction = 0) {
			let direction = Main.getDirection(direction);
		}

		while(-1) {
			let tmp = Main.getDirection(direction);

			if ( ~(tmp = 0) ) {
				let direction = tmp;
			}

			// Move the snake and draw the snake
			do Screen.clearScreen();
			do snake.move(direction);
			do snake.drawSnake();
			do Sys.wait(100);
		}

		return;
	}

	
}

